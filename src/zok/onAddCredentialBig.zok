import "hashes/blake2/blake2s" as blake2s;
import "hashes/poseidon/poseidon" as poseidon;
import "utils/pack/u32/pack128" as pack128;

// Use poseidon to hash a u32[8] value (such as a blake2s output)
// @param bh - blake2s hash
def poseidonHashU328(u32[8] bh) -> field {
    return poseidon([pack128(bh[0..4]), pack128(bh[4..8])]);
}

/* Asserts that a leaf's preimage begins with a certain address and it was constructed from a previous leaf by modifying the secret
 * This is useful because the signed leaf can be signed by the authority, but the authority must know the secret and all of its contents to sign off on it
 * With this function, the user can take that old leaf and generate a new leaf and prove everything is the same except the secret
 * This function is called whenever adding a BigCredential to the Hub
 */
def main(field signedLeaf, field newLeaf, u32[5] address, private u32[4] oldSecret, private u32[4] newSecret, private u32[7] credsPart1, private u32[16] credsPart2) {
    u32[2][16] oldPreimage = [[...address, ...oldSecret, ...credsPart1], credsPart2];
    u32[2][16] newPreimage = [[...address, ...newSecret, ...credsPart1], credsPart2];
    assert(poseidonHashU328(blake2s(oldPreimage)) == signedLeaf);
    assert(poseidonHashU328(blake2s(newPreimage)) == newLeaf);
    return;
}